apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'

def versionMajor = 2
def versionMinor = 1
def versionPatch = 5
def appVersionCode = versionMajor * 1000 + versionMinor * 100 + versionPatch

android {
    compileSdkVersion rootProject.ext.compileVersion
    defaultConfig {
        applicationId "com.ygorcesar.jamdroidfirechat"
        minSdkVersion rootProject.ext.minVersion
        targetSdkVersion rootProject.ext.targetVersion

        versionCode appVersionCode
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        resValue "string", "facebook_app_id", FacebookAppIdJamdroid
        resValue "string", "jamdroid_google_app_id", GoogleConsoleAppIdJamdroid
        resValue "string", "google_web_oauth_client_id", GoogleWebOauthId
        resValue "string", "version", "%s ${versionMajor}.${versionMinor}.${versionPatch}"
    }
    buildTypes {
        debug {
            minifyEnabled true
            useProguard false
            versionNameSuffix "-debug"
        }

        release {
            minifyEnabled true
            useProguard true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE-FIREBASE.txt'
        exclude 'META-INF/NOTICE'
    }

    dataBinding { enabled = true }
    sourceSets { main.java.srcDirs += 'src/main/kotlin' }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    kapt "com.android.databinding:compiler:$gradle_plugin_version"

    implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
    implementation "com.android.support:design:$supportLibraryVersion"
    implementation "com.android.support:cardview-v7:$supportLibraryVersion"

    /* Google Play Services */
    implementation "com.google.android.gms:play-services-auth:$playServicesVersion"
    implementation "com.google.android.gms:play-services-places:$playServicesVersion"

    /* Firebase SDK */
    implementation "com.google.firebase:firebase-core:$firebaseSdkVersion"
    implementation "com.google.firebase:firebase-database:$firebaseSdkVersion"
    implementation "com.google.firebase:firebase-auth:$firebaseSdkVersion"
    implementation "com.google.firebase:firebase-messaging:$firebaseSdkVersion"
    implementation "com.google.firebase:firebase-storage:$firebaseSdkVersion"
    implementation "com.google.firebase:firebase-invites:$firebaseSdkVersion"
    implementation("com.crashlytics.sdk.android:crashlytics:$crashlyticsVersion") {
        transitive = true
    }

    /* Facebook SDK*/
    implementation("com.facebook.android:facebook-login:$facebookSdkVersion") {
        exclude group: 'com.android.support', module: 'customtabs'
    }

    implementation("org.jetbrains.anko:anko:$ankoVersion") {
        exclude group: 'com.android.support', module: 'customtabs'
    }

    implementation 'com.firebaseui:firebase-ui-database:1.2.0'

    /* Picasso para download de imagens */
    implementation "com.github.bumptech.glide:glide:$glideVersion"

    implementation "com.google.code.gson:gson:$gsonVersion"

    /* Circle ImageView */
    implementation "de.hdodenhof:circleimageview:$circleImageviewVersion"

    implementation "com.flipboard:bottomsheet-core:$flipboardBottomSheetVersion"
    implementation "com.flipboard:bottomsheet-commons:$flipboardBottomSheetVersion"

    implementation 'com.greysonparrelli.permiso:permiso:0.3.0'
    implementation "com.github.hani-momanii:SuperNova-Emoji:$emojiVersion"

    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "com.android.support.test:runner:$runnerVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$espressoVersion"
}

apply plugin: 'com.google.gms.google-services'